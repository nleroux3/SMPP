! Estimation of the thermal and hydraulic parameters
subroutine calc_parameters(alpha_wetting)
USE Declarations
implicit none
real(dp), intent(out) ::   alpha_wetting(N-1,M-1)  ! Alpha parameter for the wetting WRC (Likos et al., 2013)
real(dp) :: inv_viscosity, &           ! Inverse of kinematic viscosity of water [s/m2]
        &   S_aep_wetting(N-1,M-1), &  ! Water saturation for AEP on wetting WRC
        &   AEP_wetting(N-1,M-1), &    ! Air entry pressure for drying WRC (de Rooij and Cho, 1999) [m]
        &   WEP(N-1,M-1)               ! Water entry pressure (Katsushima et al., 2013) [m]
inv_viscosity = 1_dp/1.787e-6_dp



do j = 1,M-1
   do i = 1,N-1

      S(i,j) = max((water_content(i,j,1)-irreducible)/(porosity(i,j)-irreducible),0._dp)  

      !================ Effective thermal conductivity Calonne et al., 2011 [W/m K] ===============================
      k_eff(i,j) = 2.5e-6_dp*(dry_density(i,j)*dry_density(i,j))-1.23e-4_dp*dry_density(i,j)+0.024_dp   
      ! ATTENTION thermal conductivity only for dry snow

      !================== Hydraulic conductivity of saturated snow (Calonne et al., 2012)  =====================
        Ks(i,j) = 9.81_dp * inv_viscosity * 3.0_dp * (grain_opt(i,j)*0.5_dp)**2 &
      & * dexp(-0.013_dp*dry_density(i,j))
      ! grain_opt is the diameter in [m]
      !================== Van Genuchten parameters  =========================================
      alpha(i,j) = 4.4e6_dp * (grain_classic(i,j)/dry_density(i,j))**(0.98_dp)    ! (Yamaguchi et al., 2012)
      nn(i,j) = 1.0_dp + 2.7e-3_dp * (grain_classic(i,j)/dry_density(i,j))**(-0.61_dp)
      mm(i,j) = 1.0_dp - 1.0_dp/nn(i,j)

      !alpha(i,j) = 30._dp*(grain_classic(i,j)*1000._dp)+12._dp    ! (Daanen and Nieber, 2009)
      !nn(i,j) = 0.8_dp*(grain_classic(i,j)*1000._dp)+3._dp
      !mm(i,j) = 1._dp-1._dp/nn(i,j)

      !alpha(i,j) = 7.3_dp*(grain_classic(i,j)*1000._dp)+1.9_dp  ! (Yamaguchi et al., 2010)
      !nn(i,j) = 15.68_dp*exp(-0.46*grain_classic(i,j)*1000._dp)+1._dp
      !mm(i,j) = 1._dp-1._dp/nn(i,j)

      alpha_wetting(i,j) = ratio_alpha * alpha(i,j)

 
write(*,*) ratio_alpha
      !=============== Water pressure (van Genuchten, 1982) =======================================
      !!!!!! Saturation should never be equal to zero to prevent divergence for the inverse of van Genuchten

      AEP_wetting(i,j) = 1._dp/alpha_wetting(i,j) * ( (1._dp-1._dp/nn(i,j))**(1._dp/nn(i,j)) + &
         &   ((2._dp - 1._dp/nn(i,j))**(2._dp - 1._dp/nn(i,j))-(2._dp - 1._dp/nn(i,j))) / &
         &   ((1._dp-nn(i,j))*(1._dp-1._dp/nn(i,j))**(1._dp-1._dp/nn(i,j))))

      WEP(i,j) = 0.0437_dp/grain_classic(i,j)*1e-3_dp + 0.01074_dp   
      S_aep_wetting(i,j) = (1._dp + (alpha_wetting(i,j)*AEP_wetting(i,j))**nn(i,j))**(-mm(i,j)) 

      if (water_content(i,j,1) .gt. irreducible .and. wrc(i,j) .ne. 2 .and. wrc(i,j) .ne. 3) then
         wrc(i,j) = 1
      elseif (S(i,j) .ge. 0.90_dp .and. wrc(i,j) .eq. 1) then
         wrc(i,j) = 2
      elseif (water_content(i,j,1) .le. irreducible) then
         wrc(i,j) = 0
      end if

      if (wrc(i,j) .eq. 0 ) then ! WEP: wrc = 0
         P(i,j) = - WEP(i,j)
      else if (wrc(i,j) .eq. 1) then! Wetting WRC : wrc = 1
         P(i,j) = -((S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j))) / alpha_wetting(i,j)   
      else if (wrc(i,j) .eq. 2) then! Drying WRC: wrc = 2
         P(i,j) = -((S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j))) / alpha(i,j) 
      else if (wrc(i,j) .eq. 3) then
         P(i,j) = -((S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j)) / alpha(i,j) - &
           & ((S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j)) / alpha(i,j) - &
           & (S_start(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j)) / alpha_wetting(i,j)) * &
           & dexp(10._dp *(S(i,j)-S_start(i,j))))
         P(i,j) = -min(-P(i,j),(S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j)) / alpha(i,j))
         P(i,j) = -max(-P(i,j),(S(i,j)**(-1.0_dp/mm(i,j)) - 1.0_dp) **(1.0_dp/nn(i,j)) / alpha_wetting(i,j))
      end if
    
      !================ Hydraulic conductivity for unsaturated snow ===============================
 
      if (S(i,j) .lt.  S_aep_wetting(i,j))  then ! Mualem-VG model 
         K(i,j) = Ks(i,j)*dsqrt(S(i,j))*(1.0_dp-(1.0_dp-S(i,j)** &
           & (1.0_dp/mm(i,j)))**mm(i,j))**2
      else
         K(i,j) = Ks(i,j)
      end if
   enddo
enddo





! ================= Estimation of the hydraulic conductivity by arithmetic mean [Belfort and Lehmann, 2005] 
! =================== and arithmetic mean for effective thermal conductivity at the cell interfaces 
if (M .gt. 2) then ! more than one snow layer
   do j = 1,M-1
      do i = 1,N-1
         if (i .ne. 1 .and. i .ne. N-1 .and. j .ne. 1 .and. j .ne. M-1) then

            !====================== Middle cells ========================
            K_south(i,j) = (K(i,j)+K(i,j-1))*(0.5_dp)
            K_north(i,j) = (K(i,j)+K(i,j+1))*(0.5_dp)
            K_east(i,j) = (K(i,j)+K(i+1,j))*(0.5_dp)
            K_west(i,j) = (K(i-1,j)+K(i,j))*(0.5_dp)

            k_eff_west(i,j) = (0.5_dp)*(k_eff(i,j)+k_eff(i-1,j))
            k_eff_east(i,j) = (0.5_dp)*(k_eff(i,j)+k_eff(i+1,j))
            k_eff_north(i,j) = (0.5_dp)*(k_eff(i,j)+k_eff(i,j+1))
            k_eff_south(i,j) = (0.5_dp)*(k_eff(i,j)+k_eff(i,j-1))
         endif

         if (i .ne. 1 .and. i .ne. N-1 .and. j .eq. 1) then  
            !============ Lower boundary except west and east cells ============
            K_east(i,1) = (K(i,1)+K(i+1,1))*(0.5_dp)
            K_west(i,1) = (K(i-1,1)+K(i,1))*(0.5_dp)
            K_north(i,1) = (K(i,1)+K(i,2))*(0.5_dp)
            K_south(i,1) = K(i,1)

            k_eff_west(i,1) = (0.5_dp)*(k_eff(i,1)+k_eff(i-1,1))
            k_eff_east(i,1) = (0.5_dp)*(k_eff(i,1)+k_eff(i+1,1))
            k_eff_north(i,1) = (0.5_dp)*(k_eff(i,1)+k_eff(i,2))
            k_eff_south(i,1) = k_eff(i,1)
         endif

         if (i .ne. 1 .and. i .ne. N-1 .and. j .eq. M-1) then   
            !======= upper boundary except west and east cells ================
            K_north(i,M-1) = K(i,M-1)
            K_south(i,M-1) = (K(i,M-1)+K(i,M-2))*(0.5_dp)
            K_east(i,M-1) = (K(i,M-1)+K(i+1,M-1))*(0.5_dp)
            K_west(i,M-1) = (K(i-1,M-1)+K(i,M-1))*(0.5_dp)

            k_eff_west(i,M-1) = (0.5_dp)*(k_eff(i,M-1)+k_eff(i-1,M-1))
            k_eff_east(i,M-1) = (0.5_dp)*(k_eff(i,M-1)+k_eff(i+1,M-1))
            k_eff_north(i,M-1) = k_eff(i,M-1)
            k_eff_south(i,M-1) = (0.5_dp)*(k_eff(i,M-1)+k_eff(i,M-2))
         endif

         if (j .ne. 1 .and. j .ne. M-1 .and. i .eq. N-1) then 
            !============ Right boundary except north and south cells ============
            if (choice_lat_BC .eq. 0) then
               K_east(N-1,j) = (K(N-1,j)+K(1,j))*0.5_dp
            else
               K_east(N-1,j) = K(N-1,j)
            endif
            K_west(N-1,j) = (K(N-2,j)+K(N-1,j))*(0.5_dp)
            K_south(N-1,j) = (K(N-1,j)+K(N-1,j-1))*(0.5_dp)
            K_north(N-1,j) = (K(N-1,j)+K(N-1,j+1))*(0.5_dp)

            k_eff_west(N-1,j) = (0.5_dp)*(k_eff(N-1,j)+k_eff(N-2,j))
            k_eff_east(N-1,j) = k_eff(N-1,j)
            k_eff_north(N-1,j) = (0.5_dp)*(k_eff(N-1,j)+k_eff(N-1,j+1))
            k_eff_south(N-1,j) = (0.5_dp)*(k_eff(N-1,j)+k_eff(N-1,j-1))
         endif

         if (j .ne. 1 .and. j .ne. M-1 .and. i .eq. 1) then
            !========= Left boundary except north and south cells ==================
            K_east(1,j) = (K(1,j)+K(2,j))*(0.5_dp)
            if (choice_lat_BC .eq. 0) then
               K_west(1,j) = (K(N-1,j)+K(1,j))*0.5_dp
            else
               K_west(1,j) = K(1,j)
            endif
            K_south(1,j) = (K(1,j)+K(1,j-1))*(0.5_dp)
            K_north(1,j) = (K(1,j)+K(1,j+1))*(0.5_dp)

            k_eff_west(1,j) = k_eff(1,j)
            k_eff_east(1,j) = (0.5_dp)*(k_eff(1,j)+k_eff(2,j))
            k_eff_north(1,j) = (0.5_dp)*(k_eff(1,j)+k_eff(1,j+1))
            k_eff_south(1,j) = (0.5_dp)*(k_eff(1,j)+k_eff(1,j-1))
         endif

         if (j .eq. 1 .and. i .eq. 1) then
            !=========== Lower left cell =========================
            K_east(1,1) = (K(1,1)+K(2,1))*(0.5_dp)
            K_north(1,1) = (K(1,1)+K(1,2))*(0.5_dp)
            if (choice_lat_BC .eq. 0) then
               K_west(1,1) = (K(N-1,1)+K(1,1))*0.5_dp
            else
               K_west(1,1) = K(1,1)
            endif
            K_south(1,1) = K(1,1)

            k_eff_west(1,1) = k_eff(1,1)
            k_eff_east(1,1) = (0.5_dp)*(k_eff(1,1)+k_eff(2,1))
            k_eff_north(1,1) = (0.5_dp)*(k_eff(1,1)+k_eff(1,2))
            k_eff_south(1,1) = k_eff(1,1)
         endif

        if (j .eq. M-1 .and. i .eq. 1) then
            !========== Upper left cell ====================
            K_south(1,M-1) = (K(1,M-1)+K(1,M-2))*(0.5_dp)
            K_east(1,M-1) = (K(1,M-1)+K(2,M-1))*(0.5_dp)
            if (choice_lat_BC .eq. 0) then
               K_west(1,M-1) = (K(N-1,M-1)+K(1,M-1))*0.5_dp
            else
               K_west(1,M-1) = K(1,M-1)
            endif
            K_north(1,M-1) = K(1,M-1)

            k_eff_west(1,M-1) = k_eff(1,M-1)
            k_eff_east(1,M-1) = (0.5_dp)*(k_eff(1,M-1)+k_eff(2,M-1))
            k_eff_north(1,M-1) = k_eff(1,M-1)
            k_eff_south(1,M-1) = (0.5_dp)*(k_eff(1,M-1)+k_eff(1,M-2))
         endif

         if (j .eq. 1 .and. i .eq. N-1) then 
            !=========== Lower right cell=======================
            K_north(N-1,1) = (K(N-1,1)+K(N-1,2))*(0.5_dp)
            K_west(N-1,1) = (K(N-2,1)+K(N-1,1))*(0.5_dp)
            if (choice_lat_BC .eq. 0) then
               K_east(N-1,1) = (K(N-1,1)+K(1,1))*(0.5_dp)
            else
               K_east(N-1,1) = K(N-1,1)
            endif
            K_south(N-1,1) = K(N-1,1)

            k_eff_west(N-1,1) = (0.5_dp)*(k_eff(N-1,1)+k_eff(N-2,1))
            k_eff_east(N-1,1) = k_eff(N-1,1)
            k_eff_north(N-1,1) = (0.5_dp)*(k_eff(N-1,1)+k_eff(N-1,2))
            k_eff_south(N-1,1) = k_eff(N-1,1)
         endif

         if (j .eq. M-1 .and. i .eq. N-1) then
            !======= Upper right cell =================
            K_south(N-1,M-1) = (K(N-1,M-1)+K(N-1,M-2))*(0.5_dp)
            K_west(N-1,M-1) = (K(N-2,M-1)+K(N-1,M-1))*(0.5_dp)
            if (choice_lat_BC .eq. 0) then
               K_east(N-1,M-1) = (K(N-1,M-1)+K(1,M-1))*(0.5_dp)
            else
               K_east(N-1,M-1) = K(N-1,M-1)
            endif
            K_north(N-1,M-1) = K(N-1,M-1)

            k_eff_west(N-1,M-1) = (0.5_dp)*(k_eff(N-1,M-1)+k_eff(N-2,M-1))
            k_eff_east(N-1,M-1) = k_eff(N-1,M-1)
            k_eff_north(N-1,M-1) = k_eff(N-1,M-1)
            k_eff_south(N-1,M-1) = (0.5_dp)*(k_eff(N-1,M-1)+k_eff(N-1,M-2))
         endif
      enddo
   enddo

else   ! M = 2, i.e. one layer
   !======== right cell =====================
   K_north(N-1,1) = K(N-1,1)
   K_west(N-1,1) = (K(N-2,1)+K(N-1,1))*(0.5_dp)
   if (choice_lat_BC .eq. 0) then
      K_east(N-1,1) = (K(N-1,1)+K(1,1))*0.5_dp
   else
      K_east(N-1,1) = K(N-1,1)
   endif
   K_south(N-1,1) = K(N-1,1)

   k_eff_west(N-1,1) = (0.5_dp)*(k_eff(N-1,1)+k_eff(N-2,1))
   k_eff_east(N-1,1) = k_eff(N-1,1)
   k_eff_north(N-1,1) = k_eff(N-1,1)
   k_eff_south(N-1,1) = k_eff(N-1,1)

   ! ============= left cell ========================
   K_east(1,1) = (K(1,1)+K(2,1))*(0.5_dp)
   K_north(1,1)= K(1,1)
   if (choice_lat_BC .eq. 0) then
      K_west(1,1) = (K(N-1,1)+K(1,1))*0.5_dp
   else
      K_west(1,1) = K(1,1)
   endif
   K_south(1,1) = K(1,1)

   k_eff_west(1,1) = k_eff(1,1)
   k_eff_east(1,1) = (0.5_dp)*(k_eff(1,1)+k_eff(2,1))
   k_eff_north(1,1) = k_eff(1,1)
   k_eff_south(1,1) = k_eff(1,1)

   !================ Middle cells ===================== 
   K_east(2:N-2,1) = (K(2:N-2,1)+K(3:N-1,1))*(0.5_dp)
   K_west(2:N-2,1) = (K(1:N-3,1)+K(2:N-2,1))*(0.5_dp)
   K_north(2:N-2,1) = K(2:N-2,1)
   K_south(2:N-2,1) = K(2:N-2,1)

   k_eff_west(2:N-2,1) = (0.5_dp)*(k_eff(2:N-2,1)+k_eff(1:N-3,1))
   k_eff_east(2:N-2,1) = (0.5_dp)*(k_eff(2:N-2,1)+k_eff(3:N-1,1))
   k_eff_north(2:N-2,1) = k_eff(2:N-2,1)
   k_eff_south(2:N-2,1) = k_eff(2:N-2,1)

endif


RETURN
end
